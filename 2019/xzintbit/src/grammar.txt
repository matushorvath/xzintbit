program = 
      line { line }
    .

line = 
      symbol EOL
    | "." directive EOL
    | instruction EOL
    .

symbol = 
    | [ "+" NUMBER "=" ] IDENTIFIER ":"
    .

directive = 
      "FRAME" identifier_list [ ";" identifier_list [ ";" identifier_list ] ]
    | "ENDFRAME"
    | "EOF"
    .

identifier_list = 
      IDENTIFIER { "," IDENTIFIER }
    .

instruction = 
      "add" in_param "," in_param "," out_param
    | "mul" in_param "," in_param "," out_param
    | "lt" in_param "," in_param "," out_param
    | "eq" in_param "," in_param "," out_param
    | "jnz" in_param "," in_param
    | "jz" in_param "," in_param
    | "arb" in_param
    | "out" in_param
    | "in" out_param
    | "hlt"
    | "cal" in_param
    | "ret" number_or_char
    | "db" db_param { "," db_param }
    | "ds" NUMBER, in_param
    .

out_param = 
      "[" [ "rb" ( "+" | "-" ) ] value "]"
    .

in_param = 
      out_param
    | value
    .

db_param = 
      value
    | STRING
    .

value = 
      number_or_char
    | IDENTIFIER [ ( "+" | "-" ) number_or_char ]
    .

number_or_char = 
      [+|-] (NUMBER|CHAR)
    .
